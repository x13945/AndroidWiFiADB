buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.intellij' version '1.3.0'
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

group 'com.github.pedrovgs'
version '2.5-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

def configPropertiesFile = project.rootProject.file("config.properties");
def configProperties = new Properties()
configProperties.load(new FileInputStream(configPropertiesFile))

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = "191.8026.42"
    type = "IC"
    pluginName = 'AndroidWiFiADB'
    plugins = ['android']
    updateSinceUntilBuild = false
}

runIde {
    // Absolute path to installed target 3.5 Android Studio to use as
    // IDE Development Instance (the "Contents" directory is macOS specific):
    ideDir = new File(configProperties['android_studio_path'])
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-all:1.10.19'
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}